if (  isModule.toBoolean()){
    apply plugin: 'com.android.application'
}else {
    apply plugin :'com.android.library'
}
apply plugin:'kotlin-android'
apply plugin: 'kotlin-kapt'


android {
    compileSdkVersion 30
    buildToolsVersion "30.0.2"

    defaultConfig {
        if ( isModule.toBoolean()){
            applicationId "com.crazy.baseimagegallery"
        }
        //项目支持最低的Android版本
        minSdkVersion 21
        //基于哪个版本的sdk开发
        targetSdkVersion 30
        //内部版本号
        versionCode 1
        //外部版本号
        versionName "1.0"

        //配置单元测试时使用的runner
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        //测试包名
//        testApplicationId "com.zy.testAppcom"
        //多dex 是否开启
        multiDexEnabled true

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [AROUTER_MODULE_NAME: project.getName()]
            }
        }
    }

    sourceSets {

        main{
            if (isModule.toBoolean()){
                manifest.srcFile 'src/main/moduleManifest/AndroidManifest.xml'
            }
            else {
                manifest.srcFile 'src/main/AndroidManifest.xml'
            }

            // 配置res文件夹，如果使用需要 需要自己手动新建再选择
            res.srcDirs = [
                    'src/main/res/layout/activity',
                    'src/main/res/layout/fragment',
                    'src/main/res/layout',
                    'src/main/res'
            ]

        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

//    packagingOptions {
//        exclude 'META-INF/services/javax.annotation.processing.Processor'
//    }

    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        viewBinding true
    }

    //lint 配置
    lintOptions {
        //启动出错停止gradle 构建
        abortOnError false
        //关闭指定问题检查
        disable 'TypographyFractions'
        //打开指定问题检查
        enable 'RtlHardcoded'
        // 仅检查指定问题
        check 'NewApi'

        //生成html报告
        htmlReport true
        

    }

    //约束资源前缀
    resourcePrefix "${project.name}_"
}

dependencies {
    api fileTree(dir: 'libs', include: ['*.jar'])

    // api 替换 implementation ：打包并依赖，解决其他module 二次依赖
    api "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    api 'androidx.appcompat:appcompat:1.2.0'
    api 'androidx.constraintlayout:constraintlayout:2.0.1'
    implementation 'androidx.core:core-ktx:1.3.1'
    api 'com.google.android.material:material:1.2.1'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.3.0'
    implementation 'androidx.navigation:navigation-ui-ktx:2.3.0'
//    implementation 'androidx.preference:preference:1.1.1'

    //test
    // Optional -- Mockito framework（可选，用于模拟一些依赖对象，以达到隔离依赖的效果）
    testImplementation 'org.mockito:mockito-core:2.19.0'
    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
    androidTestImplementation 'com.android.support:support-annotations:27.1.1'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test:rules:1.0.2'



    //dagger2
    //倒入第三方库
    app.each {
        implementation it
    }

    api "com.alibaba:arouter-api:$rootProject.arouterVersion"
    kapt "com.alibaba:arouter-compiler:$rootProject.arouterVersion"

    kapt "com.google.dagger:dagger-compiler:$rootProject.dagger2Version"
    kapt "com.google.dagger:dagger-android-processor:$rootProject.dagger2Version"
    kapt "com.github.bumptech.glide:compiler:$rootProject.glide_version"


}