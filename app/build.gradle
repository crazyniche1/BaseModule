plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'

}


android {

    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion


    defaultConfig {
        applicationId rootProject.ext.applicationId
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode appVersionCode
        versionName appVersionName

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [AROUTER_MODULE_NAME: project.getName()]
            }
        }
    }

    signingConfigs {
        release{
            keyAlias rootProject.ext.keyAlias
            keyPassword /*System.getenv("KEYSTORE_PASSWORD")*/ rootProject.ext.keyPassword
            storeFile file(rootProject.ext.storeFile)
            storePassword /*System.getenv("KEYSTORE_PASSWORD")*/ rootProject.ext.keyPassword
        }

    }

    //根据架构配置
    splits{
        abi {
            // 开启
            enable true
            //重置所有目录
            reset()

            include  'armeabi-v7a', 'arm64-v8a'
            //编译包含全部加购的App
            universalApk true
        }
    }

    buildTypes {

        release {
            //混淆编译
            minifyEnabled true
            //移除无用资源
            shrinkResources true
            //压缩
            zipAlignEnabled true
            //正式版不打印日志
            buildConfigField("boolean", "LOG_DEBUG", "false")
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            //调用签名
            signingConfig signingConfigs.release

        }

        debug {
            signingConfig signingConfigs.release
            buildConfigField("boolean", "LOG_DEBUG", "true")
        }

    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        viewBinding true
    }

//    resourcePrefix "${project.name}_"

      //自定义输出apk名
    applicationVariants.all{
        variant ->
            variant.outputs.all {
                if (variant.getBuildType().getName() == 'release') {
                    outputFileName = new File("app" + "_v" + variant.versionName + "_code" + variant.versionCode + "_" + releaseDate() + "_" + variant.getBuildType().getName() + ".apk")
                }
            }
    }
}

def static releaseDate(){
    return new Date().format("yyyy-MM-dd", TimeZone.getTimeZone("UTC"))
}



dependencies {
    api fileTree(dir: 'libs', include: ['*.jar'])
    api  project(':module_base_gallery')
    implementation  project(':rapermission')

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.media:media:1.1.0'
    implementation 'androidx.appcompat:appcompat:1.2.0'
//    implementation 'androidx.preference:preference-ktx:1.1.1'
    implementation 'com.google.android.material:material:1.2.1'

    // Kotlin + coroutines
    implementation("androidx.work:work-runtime-ktx:2.7.1")

    implementation 'org.greenrobot:eventbus:3.1.1'

    testImplementation 'org.mockito:mockito-core:2.19.0'
    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'


    api "com.alibaba:arouter-api:$rootProject.arouterVersion"
    kapt "com.alibaba:arouter-compiler:$rootProject.arouterVersion"

    kapt "com.google.dagger:dagger-compiler:$rootProject.dagger2Version"
    kapt "com.google.dagger:dagger-android-processor:$rootProject.dagger2Version"
    kapt "com.github.bumptech.glide:compiler:$rootProject.glide_version"
}